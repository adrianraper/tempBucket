<?xml version="1.0" encoding="utf-8"?>
<ielts:IELTSApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
						xmlns:s="library://ns.adobe.com/flex/spark"
						xmlns:ielts="com.clarityenglish.ielts.*"
						width="100%"
						height="100%"
						initialize="onInitialize(event)"
						applicationComplete="onApplicationComplete(event)"
						addedToStage="onAddedToStage(event)"
						applicationDPI="160"
						runtimeDPIProvider="org.davekeen.core.RetinaRuntimeDPIProvider"
						gestureSwipe="onGestureSwipe(event)">
	<fx:Script>
		<![CDATA[
			import com.clarityenglish.common.CommonNotifications;
			import com.clarityenglish.common.vo.config.BentoError;
			import com.clarityenglish.components.SpinnerDropDownList;
			import com.clarityenglish.ieltsair.IELTSAirApplicationFacade;
			import com.clarityenglish.textLayout.conversion.XHTMLImporter;
			import com.clarityenglish.textLayout.elements.SelectElement;
			import com.clarityenglish.textLayout.stylesheets.applicators.CSSApplicator;
			
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;
			
			import org.davekeen.delegates.RemoteDelegate;
			
			import skins.bento.components.SpinnerDropDownListSkin;
			
			import spark.components.DropDownList;
			
			// TODO: This is duplicated in IELTS and Rotterdam!!  Refactor!!
			protected function onInitialize(event:FlexEvent):void {
				// Determine what platform we are running on
				var platform:String = Capabilities.version.split(" ")[0];
				var language:String = Capabilities.language;

				if (language != "en" && language != "zh-CN") {
					language = "en";
				}
				
				switch (platform) {
					case "IOS":
						// Set the iOS config xml file
						FlexGlobals.topLevelApplication.parameters.configFile = "ios-config-" + language + ".xml";
						XHTMLImporter.media = "ios";
						break;
					case "AND":
						// Set the Android config xml file as well as getting the prefix, product code and font size from the iOS settings
						FlexGlobals.topLevelApplication.parameters.configFile = "android-config-" + language + ".xml";
						XHTMLImporter.media = "android";
						break
					default:
						throw new Error("Unsupported platform - " + Capabilities.version);
				}
				
				// Hardcode the font offset to +4 for tablet GH #55
				CSSApplicator.fontSizeOffset = 4;
				
				// The AIR application uses a custom Spinner instead of the normal dropdown
				SelectElement.elementFactoryFunction = function():DropDownList {
					var dropDownList:DropDownList = new SpinnerDropDownList();
					dropDownList.setStyle("skinClass", SpinnerDropDownListSkin);
					return dropDownList;
				}
				
				// #383 No busy cursor on AIR
				RemoteDelegate.setShowBusyCursor(false);
				
				// #444 Use gestures for swiping (may eventually be used in the help screen)
				Multitouch.inputMode = MultitouchInputMode.GESTURE;
				
				// #479 For the AIR version we get the version number out of the xml descripter
				var descriptor:XML = NativeApplication.nativeApplication.applicationDescriptor;
				var ns:Namespace = descriptor.namespace();
				versionNumber = descriptor.ns::versionNumber.toString();
				
				// Set the facade
				facade = IELTSAirApplicationFacade.getInstance();
			}
			
			protected function onApplicationComplete(event:FlexEvent):void {
				FlexGlobals.topLevelApplication.loaderInfo.uncaughtErrorEvents.addEventListener(UncaughtErrorEvent.UNCAUGHT_ERROR, onUncaughtError);
			}
			
			protected function onAddedToStage(event:Event):void {
				/*if (!logTextArea.visible) { 
					logTextArea.visible = true;
					
					// Configure logging
					var logTarget:TextAreaLogTarget = new TextAreaLogTarget(logTextArea);
					logTarget.filters = [ "com.*", "org.*", "AnswerableBehaviour.*" ];
					logTarget.level = LogEventLevel.ALL;
					logTarget.includeDate = false;
					logTarget.includeTime = false;
					logTarget.includeCategory = true;
					logTarget.includeLevel = true;
					Log.addTarget(logTarget);
				}*/
			}
			
			protected function onUncaughtError(event:UncaughtErrorEvent):void {
				var bentoError:BentoError = new BentoError();
				
				if (event.error is Error) {
					log.error((event.error as Error).message + " - " + (event.error as Error).getStackTrace());
				} else if (event.error is ErrorEvent) {
					log.error((event.error as ErrorEvent).toString());
				} else {
					log.error("Unknown: " + event.error);
				}
			}
			
			protected function onGestureSwipe(event:TransformGestureEvent):void {
				if (facade)
					facade.sendNotification((event.offsetX < 1 ? CommonNotifications.GESTURE_SWIPE_LEFT : CommonNotifications.GESTURE_SWIPE_RIGHT), event.offsetX);
			}
			
		]]>
	</fx:Script>
	
	<fx:Style>
		global {
			fontFamily: Helvetica;
			fontSize: 13;
		}
	</fx:Style>
	
	<fx:Style source="ielts.css" />
	<fx:Style source="ieltsair.css" />
	
</ielts:IELTSApplication>