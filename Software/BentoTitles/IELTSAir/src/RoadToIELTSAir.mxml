<?xml version="1.0" encoding="utf-8"?>
<ielts:IELTSApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
						xmlns:s="library://ns.adobe.com/flex/spark"
						xmlns:ielts="com.clarityenglish.ielts.*"
						width="100%"
						height="100%"
						initialize="onInitialize(event)"
						applicationComplete="onApplicationComplete(event)"
						addedToStage="onAddedToStage(event)"
						applicationDPI="160"
						runtimeDPIProvider="RetinaRuntimeDPIProvider"
						gestureSwipe="onGestureSwipe(event)"
						checkNetworkAvailabilityUrl="http://www.google.com">
	<fx:Script>
		<![CDATA[
			import com.clarityenglish.bento.view.xhtmlexercise.components.XHTMLExerciseView;
			import com.clarityenglish.common.vo.config.BentoError;
			import com.clarityenglish.components.SpinnerDropDownList;
			import com.clarityenglish.ieltsair.IELTSAirApplicationFacade;
			import com.clarityenglish.ieltsair.IELTSAirNotifications;
			import com.clarityenglish.textLayout.elements.SelectElement;
			import com.clarityenglish.textLayout.stylesheets.applicators.CSSApplicator;
			import com.flashiphonedevelopment.settings.IPhoneAppSettings;
			
			import flash.utils.setTimeout;
			
			import mx.core.FlexGlobals;
			import mx.core.mx_internal;
			import mx.events.FlexEvent;
			
			import org.davekeen.delegates.RemoteDelegate;
			
			import skins.bento.components.SpinnerDropDownListSkin;
			
			import spark.components.DropDownList;
			import spark.components.supportClasses.StyleableTextField;
			
			protected function onInitialize(event:FlexEvent):void {
				// Set the iOS config xml file as well as getting the prefix and product code from the iOS settings
				FlexGlobals.topLevelApplication.parameters.configFile = "ios-config.xml";
				try { FlexGlobals.topLevelApplication.parameters.prefix = IPhoneAppSettings.getInstance().stringForKey("prefix"); } catch (e:TypeError) { }
				try { FlexGlobals.topLevelApplication.parameters.productCode = IPhoneAppSettings.getInstance().stringForKey("productVersion"); } catch (e:TypeError) { }
				
				// The AIR application applies an offset to font sizes in the xhtml files  
				try { CSSApplicator.fontSizeOffset = new Number(IPhoneAppSettings.getInstance().stringForKey("fontOffset")); } catch (e:TypeError) { }
				
				// The AIR application uses a custom Spinner instead of the normal dropdown
				SelectElement.elementFactoryFunction = function():DropDownList {
					var dropDownList:DropDownList = new SpinnerDropDownList();
					dropDownList.setStyle("skinClass", SpinnerDropDownListSkin);
					return dropDownList;
				}
				
				// #383 No busy cursor on AIR
				RemoteDelegate.setShowBusyCursor(false);
				
				// #444 Use gestures for swiping (may eventually be used in the help screen)
				Multitouch.inputMode = MultitouchInputMode.GESTURE;
				
				// #376 (not sure if it helps though)
				XHTMLExerciseView.useCacheAsBitmap = true;
				
				// #479 For the AIR version we get the version number out of the xml descripter
				var descriptor:XML = NativeApplication.nativeApplication.applicationDescriptor;
				var ns:Namespace = descriptor.namespace();
				versionNumber = descriptor.ns::versionNumber.toString();
				
				// Start IELTS!
				facade = IELTSAirApplicationFacade.getInstance();
			}
			
			protected function onApplicationComplete(event:FlexEvent):void {
				FlexGlobals.topLevelApplication.loaderInfo.uncaughtErrorEvents.addEventListener(UncaughtErrorEvent.UNCAUGHT_ERROR, onUncaughtError);
			}
			
			override protected function onAddedToStage(event:Event):void {
				super.onAddedToStage(event);
				
				/*if (!logTextArea.visible) {
					logTextArea.visible = true;
					
					// Configure logging
					var logTarget:TextAreaLogTarget = new TextAreaLogTarget(logTextArea);
					logTarget.filters = [ "com.*", "org.*", "AnswerableBehaviour.*" ];
					logTarget.level = LogEventLevel.ALL;
					logTarget.includeDate = false;
					logTarget.includeTime = false;
					logTarget.includeCategory = true;
					logTarget.includeLevel = true;
					Log.addTarget(logTarget);
				}*/
			}
			
			protected function onUncaughtError(event:UncaughtErrorEvent):void {
				var bentoError:BentoError = new BentoError();
				
				if (event.error is Error) {
					log.error((event.error as Error).message + " - " + (event.error as Error).getStackTrace());
				} else if (event.error is ErrorEvent) {
					log.error((event.error as ErrorEvent).toString());
				} else {
					log.error("Unknown: " + event.error);
				}
			}
			
			protected function onGestureSwipe(event:TransformGestureEvent):void {
				if (facade)
					facade.sendNotification((event.offsetX < 1 ? IELTSAirNotifications.GESTURE_SWIPE_LEFT : IELTSAirNotifications.GESTURE_SWIPE_RIGHT), event.offsetX);
			}
			
		]]>
	</fx:Script>
	
	<fx:Style>
		@namespace s "library://ns.adobe.com/flex/spark";
		@namespace ielts "com.clarityenglish.ielts.*";
		
		@font-face {
			src: url("skins/ielts/assets/HelveticaNeueLTStd-Roman.otf");
			fontFamily: HelveticaNeueLtStd;
			fontWeight: normal;
			fontAntiAliasType: advanced;
			fontSharpness: 400;
			fontThickness: -200;
			embedAsCFF: false;
		}
		
		@font-face {
			src: url("skins/ielts/assets/HelveticaNeueLTStd-Bd.otf");
			fontFamily: HelveticaNeueLtStd;
			fontWeight: bold;
			fontAntiAliasType: advanced;
			fontSharpness: 400;
			fontThickness: -200;
			embedAsCFF: false;
		}
		
	</fx:Style>
	
	<fx:Style source="ielts.css" />
	<fx:Style source="ieltsair.css" />
	
</ielts:IELTSApplication>