<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
		   xmlns:view="com.clarityenglish.resultsmanager.view.management.components.*"
		   xmlns:ui="com.clarityenglish.resultsmanager.view.shared.ui.*"
		   implements="com.clarityenglish.common.view.interfaces.CopyReceiver">
	<mx:Script>
		<![CDATA[
			import com.clarityenglish.common.events.EmailEvent;
			import com.clarityenglish.common.model.interfaces.CopyProvider;
			import com.clarityenglish.common.vo.content.Title;
			import com.clarityenglish.common.vo.email.TemplateDefinition;
			import com.clarityenglish.common.vo.manageable.Manageable;
			import com.clarityenglish.common.vo.manageable.User;
			import com.clarityenglish.common.vo.tests.ScheduledTest;
			import com.clarityenglish.resultsmanager.Constants;
			import com.clarityenglish.resultsmanager.view.management.events.ReportEvent;
			import com.clarityenglish.resultsmanager.view.management.ui.TestadminReportWindow;
			import com.clarityenglish.resultsmanager.view.shared.events.TestEvent;
			import com.clarityenglish.testadmin.view.login.components.FlatButtonSkin;
			import org.davekeen.utils.DateUtils;
			import com.adobe.serialization.json.JSON;
			import com.adobe.serialization.json.JSONDecoder;
			import com.adobe.serialization.json.JSONEncoder;
			
			import flash.text.StyleSheet;
			
			import mx.controls.Alert;
			import mx.core.UIComponent;
			import mx.events.CloseEvent;
			import mx.events.ListEvent;
			import mx.managers.PopUpManager;
			
			import org.davekeen.utils.ClassUtils;
		
			private var copyProvider:CopyProvider;
			
			public var manageables:Array;
			
			private var reportWindow:TestadminReportWindow;
			
			public var defaultLanguage:String;
			public var productCode:String;
							
			[Bindable]
			public var selectedGroup:Manageable;
			public var selectedTitle:Title;
			
			// gh#1499
			[Bindable]
			public var licencesPurchased:uint;
			[Bindable]
			public var licencesUsed:uint;
			public var _licencesScheduled:uint;
			[Bindable]
			public var licencesNeeded:int;
			
			// Icon for the alert window
			[Embed(source="/../assets/warning_icon.swf")]
			private var alertWarningIcon:Class;	
			
			public function initLicencesUsed(purchased:uint, used:uint, scheduled:uint):void {
				licencesPurchased = purchased;
				licencesUsed = used;
				licencesScheduled = scheduled;
				licencesNeeded = (licencesUsed + _licencesScheduled) - licencesPurchased;
				var licencesLeft:int = (licencesPurchased - licencesUsed);
				var replaceObj:Object = {"licencesScheduled": _licencesScheduled.toString(), 
					"licencesAvailable": (licencesLeft <= 0) ? '0' : licencesLeft.toString(),
					"licencesNeeded" : licencesNeeded.toString()};
				licencesNeededLabel.text = copyProvider.getCopyForId("licencesNeededWarning", replaceObj);
				buyMoreButton.label = copyProvider.getCopyForId('buyMoreLicences', replaceObj);
				
				// ctp#206 Are you allowed to schedule a test?
				if (licencesUsed >= licencesPurchased) {
					newTestButton.enabled = false;
				} else {
					newTestButton.enabled = true;					
				}				
			}
			public function set licencesScheduled(value:uint):void {
				_licencesScheduled = value;
				initLicencesUsed(licencesPurchased, licencesUsed, value);
			}
			[Bindable]
			public function get licencesScheduled():uint {
				return _licencesScheduled;
			}
			
			public function setCopyProvider(copyProvider:CopyProvider):void {
				this.copyProvider = copyProvider;
				
				testDetailPanel.setCopyProvider(copyProvider);
				//listOfTestsLabel.text = copyProvider.getCopyForId("listOfTestsLabel");
				newTestButton.label = copyProvider.getCopyForId("addTest");
				selectGroupLabel.text = copyProvider.getCopyForId("selectGroup");
				deleteTestButton.label = copyProvider.getCopyForId("deleteTest");
				//releaseTestButton.label = copyProvider.getCopyForId("releaseTest");
				
				//firstStepWindow.title = copyProvider.getCopyForId("testAdminFirstStep");
				licenceUseLabel.text = copyProvider.getCopyForId("licenceUse");
				licencesScheduledLabel.text = copyProvider.getCopyForId("licencesScheduled");
				licencesUsedLabel.text = copyProvider.getCopyForId("licencesUsed");
				licencesPurchasedLabel.text = copyProvider.getCopyForId("licencesPurchased");

				generateReportButton.label = copyProvider.getCopyForId("generateReport");
				sendWelcomeEmailButton.label = copyProvider.getCopyForId("sendWelcomeEmail");
				sendResultEmailButton.label = copyProvider.getCopyForId("sendResultEmail");
			}
			
			private function onSendWelcomeEmail(e:Event):void {
				// TODO where should this be set. rm.xml, config.xml?
				var test:ScheduledTest = testList.selectedItem as ScheduledTest;
				var welcomeEmailTemplate:TemplateDefinition = new TemplateDefinition();
				welcomeEmailTemplate.filename = "user/DPT-welcome";
				welcomeEmailTemplate.name = "invitation";
				
				// ctp#214 Need to send the test administrator details for the email too
				//var administrator:Object = {name: Constants.userName, email: Constants.email};
				welcomeEmailTemplate.data = {test: test, emailDetails: JSON.decode(test.emailInsertion)};

				showSendEmailDialog(function(e:CloseEvent):void {
					if (e.detail == Alert.YES)
						dispatchEvent(new EmailEvent(EmailEvent.PREVIEW_EMAIL, welcomeEmailTemplate, new Array(selectedGroup)));
				}, welcomeEmailTemplate );
			}
			// This function will probably be suppressed
			private function onSendResultEmail(e:Event):void {
				// TODO where should this be set. rm.xml, config.xml?
				var test:ScheduledTest = testList.selectedItem as ScheduledTest;
				var resultEmailTemplate:TemplateDefinition = new TemplateDefinition();
				resultEmailTemplate.filename = (test.showResult) ? "user/DPT-with-result" : "user/DPT-without-result";
				resultEmailTemplate.name = "result";
				resultEmailTemplate.data = {test: test};
				
				showSendEmailDialog(function(e:CloseEvent):void {
					if (e.detail == Alert.YES)
						dispatchEvent(new EmailEvent(EmailEvent.SEND_EMAIL, resultEmailTemplate, new Array(selectedGroup)));
				}, resultEmailTemplate );
			}
			private function showSendEmailDialog(alertListener:Function, template:TemplateDefinition):void {
				var userCount:uint = 0;
				var usersWithoutEmail:uint = 0;
				var manageables:Array = selectedGroup.getSubUsers();
				
				// Count how many users, and note how many don't have emails
				for each (var manageable:Manageable in manageables) {
					if (manageable is User) {
						if ((manageable as User).email) {
							userCount++;
						} else {
							usersWithoutEmail++;
						}
					}
				}
				var alertVersion:String = (usersWithoutEmail == 0) ? "sendEmailAlert" : "sendEmailWarnAboutNoEmail";
				var text:String = copyProvider.getCopyForId(alertVersion, {userCount: userCount, emailType: template.name, noEmailCount: usersWithoutEmail});
				Alert.show(text, "Confirm", Alert.YES | Alert.NO, this, alertListener, alertWarningIcon);
			}
			
			// Show limited report filtering window
			public function onGenerateReport(e:Event):void {
				if (!reportWindow) {
					reportWindow = PopUpManager.createPopUp(UIComponent(parentApplication), TestadminReportWindow, true) as TestadminReportWindow;
				} else {
					PopUpManager.addPopUp(reportWindow, UIComponent(parentApplication), true);
				}
				
				reportWindow.setCopyProvider(copyProvider);
				reportWindow.parentView = this;
				reportWindow.onReportables = new Array(selectedTitle);
				// gh#1523
				var selectedTest:ScheduledTest = testList.selectedItem as ScheduledTest;
				reportWindow.forReportables = new Array(selectedGroup, selectedTest); 
				PopUpManager.centerPopUp(reportWindow);
			}
				
			private function onTestChange(e:ListEvent):void {
				var selectedTest:ScheduledTest = testList.selectedItem as ScheduledTest;
				testDetailPanel.setDataProvider(selectedTest);
				resetButtons();
			}
			
			private function onNewTestButtonClick(e:Event):void {				
				var newScheduledTest:ScheduledTest = new ScheduledTest();
				newScheduledTest.groupId = selectedGroup.id;
				// gh#1523 Fake a testId to allow this to be in a List (to do with Reportable.uid)
				newScheduledTest.testId = '1';
				
				newScheduledTest.productCode = productCode;
				newScheduledTest.language = defaultLanguage;
				
				// defaults for a new test
				newScheduledTest.caption = "new test";
				newScheduledTest.status = ScheduledTest.STATUS_PRERELEASE;
				var today:Date = new Date();
				today.hours = 9;
				today.minutes = 0;			
				newScheduledTest.openTime = DateUtils.dateAndTimeToString(today);
				var later:Date = new Date();
				later.hours = 17;
				later.minutes = 0;			
				newScheduledTest.closeTime = DateUtils.dateAndTimeToString(later);
				
				// Put it temporarily into the list
				testList.dataProvider.addItem(newScheduledTest);
				testList.selectedIndex++;
				
				testDetailPanel.setDataProvider(newScheduledTest);
				testDetailPanel.visible = true;			
			}
			
			private function onDeleteTestButtonClick(e:Event):void {
				var selectedTest:ScheduledTest = testList.selectedItem as ScheduledTest;
				// If this test is not really saved, delete just removes it from the list
				switch (selectedTest.status) {
					case ScheduledTest.STATUS_PRERELEASE:
						var currentSelectedIndex:int = testList.selectedIndex;
						testList.dataProvider.removeItemAt(currentSelectedIndex);
						testList.selectedIndex = currentSelectedIndex - 1;
						break;
					// Delete the test, and if it is active reduce the scheduled test count
					case ScheduledTest.STATUS_OPEN:
					case ScheduledTest.STATUS_CLOSED:
					case ScheduledTest.STATUS_RELEASED:
					default:
						if (selectedTest.status != ScheduledTest.STATUS_CLOSED)
							clearLicencesScheduled();
						selectedTest.status = ScheduledTest.STATUS_DELETED;
						dispatchEvent(new TestEvent(TestEvent.DELETE, selectedTest));
				}
				resetSelection();
			}
			
			/*
			private function onReleaseTestButtonClick(e:Event):void {
				var selectedTest:ScheduledTest = testList.selectedItem as ScheduledTest;
				selectedTest.status = ScheduledTest.STATUS_RELEASED;
				dispatchEvent(new TestEvent(TestEvent.UPDATE, selectedTest));
			}
			*/
			// gh#1499
			public function addLicencesScheduled():void {
				licencesScheduled += selectedGroup.userCount;
				//if (licencesNeeded > 0) {
				//	var text:String = copyProvider.getCopyForId("notEnoughLicences", {licencesAvailable: (licencesPurchased - licencesUsed), licencesScheduled: licencesScheduled});
				//	Alert.show(text, "Please note", Alert.OK, this, null, alertWarningIcon);
				//}
			}
			// gh#1499 Reduce the number of scheduled tests
			// As a test can be deleted at any point, this makes the scheduled count an estimate if the test is active
			// so you don't need a complex counting basis
			public function clearLicencesScheduled():void {
				licencesScheduled -= selectedGroup.userCount;
			}
			
			public function resetButtons():void {
				newTestButton.visible = newTestButton.includeInLayout = true;
				var selectedTest:ScheduledTest = testList.selectedItem as ScheduledTest;

				if (selectedTest) {
					// You can send out the result email once the test is closed
					//sendResultEmailButton.visible = sendResultEmailButton.includeInLayout = selectedTest.isTestClosed();

					// You can release a prerelease test
					//releaseTestButton.visible = releaseTestButton.includeInLayout = selectedTest.isTestDraft();
					
					// You can send out the invitation email anytime before it closes 
					sendWelcomeEmailButton.visible = sendWelcomeEmailButton.includeInLayout = (selectedTest.isTestReleased() && !selectedTest.isTestClosed());
					
					// You can see the results any time after it opens 
					generateReportButton.visible = generateReportButton.includeInLayout = selectedTest.isTestStarted();
					
					// You can delete a test if it was set for the currently selected group
					// This is to stop you deleting a test set for a parent when you are in a child
					// You can delete a test at any time. 
					if (selectedGroup)
					    deleteTestButton.visible = deleteTestButton.includeInLayout = (selectedTest.groupId == selectedGroup.id);
				} else {
					sendWelcomeEmailButton.visible = sendWelcomeEmailButton.includeInLayout = false;
					generateReportButton.visible = generateReportButton.includeInLayout = false;
					deleteTestButton.visible = deleteTestButton.includeInLayout = false; 
				}
				if (selectedGroup) {
					//testListLabel.text = copyProvider.getCopyForId(("listOfTestsLabel"), {"name": selectedGroup.name});
					//actionsLabel.text = copyProvider.getCopyForId(("actionLabel"));
				}

			}
			public function resetSelection():void {
				onTestChange(null);
			}
			public function showCaption(item:Object):String {
				switch (item.status) {
					case ScheduledTest.STATUS_PRERELEASE:
						var statusString:String = 'not saved';
						break;
					case ScheduledTest.STATUS_RELEASED:
						statusString = 'ready';
						break;
					case ScheduledTest.STATUS_OPEN:
						statusString = 'in progress';
						break;
					case ScheduledTest.STATUS_CLOSED:
						statusString = 'complete';
						break;
					// This is not anticipated being used
					case ScheduledTest.STATUS_DELETED:
						statusString = 'archived';
						break;
				}	 
				return item.caption + " (" + statusString + ")"; 
			}
						
		]]>
	</mx:Script>
	<!--
				
	-->
	<mx:HBox width="100%" height="100%" borderStyle="none" paddingLeft="4" paddingRight="4" paddingBottom="4" horizontalGap="10">
		<mx:VBox width="30%" height="100%" >
			<view:ManageablesView id="manageablesView" width="100%" height="100%"/>
			<mx:VBox width="100%" minHeight="100" backgroundColor="#ffffff" borderStyle="solid" horizontalAlign="center" 
					 paddingTop="10" paddingBottom="10" paddingLeft="20" paddingRight="20">
				<mx:Label id="licenceUseLabel" width="100%" styleName="subHeadingLabelBlack" textAlign="center"/>
				<mx:VBox width="100%" borderStyle="solid" borderColor="#00a79d" cornerRadius="6" paddingTop="10" paddingBottom="10" paddingLeft="10" paddingRight="10">
					<mx:HBox width="100%">
						<mx:Label id="licencesPurchasedLabel" width="150"/>
						<mx:Label text="{licencesPurchased.toString()}" textAlign="right" />
					</mx:HBox>
					<mx:HBox width="100%">
						<mx:Label id="licencesUsedLabel"  width="150"/>
						<mx:Label text="{licencesUsed.toString()}"  textAlign="right"/>
					</mx:HBox>
					<mx:HBox width="100%">
						<mx:Label id="licencesScheduledLabel" width="150" />
						<mx:Label text="{licencesScheduled.toString()}"  textAlign="right"/>
					</mx:HBox>
				</mx:VBox>
				<mx:Spacer height="8" visible="{licencesNeeded &gt; 0}" includeInLayout="{licencesNeeded &gt; 0}"/>
				<mx:VBox width="100%" backgroundColor="#D95982" cornerRadius="6" 
						 paddingTop="10" paddingBottom="10" paddingLeft="10" paddingRight="10"
						 visible="{licencesNeeded &gt; 0}" includeInLayout="{licencesNeeded &gt; 0}">
					<mx:Text id="licencesNeededLabel" color="#ffffff" width="100%"/>
				</mx:VBox>
				<mx:Spacer height="8" />
				<mx:Button id="buyMoreButton" height="30" visible="{licencesNeeded &gt; 0}"
						   overSkin="{FlatButtonSkin}" upSkin="{FlatButtonSkin}" downSkin="{FlatButtonSkin}"
						   textRollOverColor="#FFFFFF"/>
			</mx:VBox>
		</mx:VBox>
		<mx:HBox width="70%" height="100%" horizontalGap="10">
			<mx:VBox minWidth="250" height="100%" >
				<mx:Spacer height="44" />
				<mx:Text id="selectGroupLabel" width="280" height="100%" visible="{selectedGroup == null}" includeInLayout="{selectedGroup == null}" styleName="subHeadingLabelBlack" textAlign="center" />
				<mx:VBox width="100%" height="100%" horizontalAlign="center" verticalGap="0" visible="{selectedGroup != null}" includeInLayout="{selectedGroup != null}">					
					<!-- <mx:Label id="testListLabel" styleName="subHeadingLabelBlack" textAlign="center"/> -->
					<mx:Button id="newTestButton" visible="true" includeInLayout="true" width="100%" height="30" click="onNewTestButtonClick(event)" 
							   overSkin="{FlatButtonSkin}" upSkin="{FlatButtonSkin}" downSkin="{FlatButtonSkin}" disabledSkin="{FlatButtonSkin}"
							   textRollOverColor="0xFFFFFF"
							   styleName="flatButton" />
					<mx:Spacer height="10" />
					<mx:List id="testList" width="100%" height="100%" change="onTestChange(event)" selectionColor="#2bb673" rollOverColor="#00a79d"
							 textSelectedColor="#ffffff" labelFunction="showCaption" verticalAlign="middle" rowHeight="32" paddingLeft="16" 
							 styleName="testList">
						<!-- 
						<mx:itemRenderer>
							<mx:Component>
								<mx:HBox height="32" verticalAlign="middle" paddingLeft="16">
									<mx:Label text="{outerDocument.showCaption(data)}" styleName="testItemList"/>
								</mx:HBox>
							</mx:Component>
						</mx:itemRenderer>
						-->
					</mx:List>
				</mx:VBox>
				<mx:Spacer height="10" />
				<mx:VBox width="100%" horizontalAlign="center" verticalGap="10" visible="{selectedGroup != null}" includeInLayout="{selectedGroup != null}">					
					<!-- <mx:Label id="actionsLabel" styleName="subHeadingLabelBlack" textAlign="center"/> -->
					<mx:Button id="generateReportButton" visible="false" includeInLayout="false" width="100%" height="30" click="onGenerateReport(event)" 
							   overSkin="{FlatButtonSkin}" upSkin="{FlatButtonSkin}" downSkin="{FlatButtonSkin}"
							   textRollOverColor="#FFFFFF" styleName="testList"/>
					<mx:Button id="sendWelcomeEmailButton" visible="false" includeInLayout="false" width="100%" height="30" click="onSendWelcomeEmail(event)" 
							   overSkin="{FlatButtonSkin}" upSkin="{FlatButtonSkin}" downSkin="{FlatButtonSkin}"
							   textRollOverColor="#FFFFFF" styleName="testList"/>
					<mx:Button id="sendResultEmailButton" visible="false" includeInLayout="false" width="100%" height="30" click="onSendResultEmail(event)" 
							   overSkin="{FlatButtonSkin}" upSkin="{FlatButtonSkin}" downSkin="{FlatButtonSkin}"
							   textRollOverColor="#FFFFFF" styleName="testList"/>
					<!-- <mx:Button id="releaseTestButton" visible="false" includeInLayout="false" width="100%" height="24" click="onReleaseTestButtonClick(event)" /> -->
					<mx:Button id="deleteTestButton" visible="false" includeInLayout="false" width="100%" height="30" click="onDeleteTestButtonClick(event)" 
							   overSkin="{FlatButtonSkin}" upSkin="{FlatButtonSkin}" downSkin="{FlatButtonSkin}"
							   textRollOverColor="#FFFFFF" styleName="testList"/>
				</mx:VBox>
			</mx:VBox>
			<ui:TestDetailPanel width="450" height="100%" id="testDetailPanel" visible="{testList.selectedItem != null}" includeInLayout="{testList.selectedItem != null}" />
			<!--
			<mx:VBox width="480" visible="{selectedGroup != null}" includeInLayout="{selectedGroup != null}" >
			</mx:VBox>
			-->
		</mx:HBox>
	</mx:HBox>
</mx:Canvas>